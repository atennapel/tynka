import "lib/Base" (...)
import "lib/JVM" (...)
import "lib/Meta" (ListM, foldrListM, elimListM, NilM, ConsM)

def CurryM (S : ListM VTy) (A : VTy) =
  foldrListM ^A (\hd tl. ^hd -> tl) S

def CurryMM (S : ListM VTy) (A : Meta) =
  foldrListM A (\hd tl. ^hd -> tl) S

def TupleM (S : ListM VTy) =
  foldrListM () (\hd tl. ^hd ** tl) S

def CurryC (S : ListM VTy) (A : CTy) : CTy =
  foldrListM A (\hd tl. hd -> tl) S

def CurryS (S : ListM VTy) (A : VTy) : CTy =
  foldrListM (Unit -> A) (\hd tl. hd -> tl) S

def returnCurryS {S : ListM VTy} {A : VTy} (x : A) : CurryS S A =
  elimListM (\S'. CurryS S' A)
    (\_. x)
    (\_ _ ind _. ind)
    S

def TupleS (S : ListM VTy) (A : VTy) : VTy =
  foldrListM A (\hd tl. Pair hd tl) S

def lastTupleS {S : ListM VTy} {A} (t : TupleS S A) : A =
  elimListM (\S. TupleS S A -> A)
    (\x. x)
    (\_ _ ind t. ind t.2)
    S t 

def StateN (S : ListM VTy) (A : VTy) = CurryS S (TupleS S A)

def StateN' (S S' : ListM VTy) (A : VTy) = CurryS S (TupleS S' A)

def run {S A} (c : StateN S A) : CurryM S (TupleS S A) =
  elimListM (\S'. StateN' S' S A -> CurryM S' (TupleS S A))
    (\c. c U)
    (\_ _ ind c a. ind (c a))
    S c

def exec {S A} (c : StateN S A) : CurryM S A =
  elimListM (\S'. StateN' S' S A -> CurryM S' A)
    (\c. lastTupleS (c U))
    (\_ _ ind c a. ind (c a))
    S c

def return {S} {A : VTy} (a : A) : StateN S A =
  elimListM (\S'. (TupleS S' A -> TupleS S A) -> StateN' S' S A)
    (\up _. up a)
    (\A _ ind up s. ind (\t. up (MkPair s t)))
    S (\x. x)

def applyTupleS {S} {A B} (t : TupleS S A) (f : A -> StateN S B) : TupleS S B =
  elimListM (\S'. TupleS S' A -> (A -> StateN' S' S B) -> TupleS S B)
    (\v f. f v U)
    (\_ _ ind t f. ind (snd t) (\v. f v (fst t)))
    S t f

def bind {S} {A B : VTy} (c : StateN S A) (f : A -> StateN S B) : StateN S B =
  elimListM (\S'. StateN' S' S A -> StateN' S' S B)
    (\t _. let res := t U; applyTupleS res f)
    (\_ _ ind c s. ind (c s))
    S c

def map {S} {A B : VTy} (f : A -> B) (c : StateN S A) : StateN S B =
  bind c \x. return (f x)

def seq {S} {A B : VTy} (c : StateN S A) (f : StateN S B) : StateN S B =
  bind c \_. f

def consTupleCurryS {S} {A R : VTy} (a : A) (c : CurryS S (TupleS S R)) : CurryS S (TupleS (ConsM A S) R) =
  elimListM (\S'. CurryS S' (TupleS S R) -> CurryS S' (TupleS (ConsM A S) R))
    (\r _. MkPair a (r U))
    (\_ _ ind c s. ind (c s))
    S c

def get {S} : StateN S (TupleS S Unit) =
  elimListM (\S'. (TupleS S' Unit -> TupleS S Unit) -> StateN S' (TupleS S Unit))
    (\f _. f U)
    (\_ _ ind up s. consTupleCurryS s (ind \x. up (MkPair s x)))
    S (\x. x)

def put {S} : CurryC S (StateN S Unit) =
  elimListM (\S'. (TupleS S' Unit -> TupleS S Unit) -> CurryC S' (StateN S Unit))
    (\f. returnCurryS (f U))
    (\_ _ ind up s. ind (\x. up (MkPair s x)))
    S (\x. x)

def fromTupleM {S} (t : TupleM S) : TupleS S Unit =
  elimListM (\S'. TupleM S' -> (TupleS S' Unit -> TupleS S Unit) -> TupleS S Unit)
    (\_ f. f U)
    (\_ _ ind t f. ind t.2 (\x. f (MkPair t.1 x)))
    S t (\x. x)

def update {S} (f : CurryMM S (TupleM S)) : StateN S Unit =
  elimListM (\S'. CurryMM S' (TupleM S) -> StateN' S' S Unit)
    (\vs _. fromTupleM vs)
    (\_ _ ind f s. ind (f s))
    S f

def loopN {S} {A : VTy} (n : Int) (action : StateN S A) : StateN S Unit =
  fix (rec n.
    if leqInt n 0 then return {S} U
    else do {bind}
      action;
      rec (subInt n 1)
  ) n
